#!/usr/bin/env zsh
#######################################################################
# Fern's Dotfiles -- ZLE Widget - Prepend `sudo` to current command
# https://github.com/fernzi/dotfiles
#######################################################################

# I constantly forget that I've got this widget,
# and end up pressing home and typing it by hand.

if [[ ! -v __zsh_sudo_cmd ]]; then
	typeset -g __zsh_sudo_cmd=sudo

	if [[ -v commands[doas] ]] && doas -C /etc/doas.conf 2>&-; then
		# Use `doas` when it's available and correctly configured,
		# For OpenBSD and the weirdos who use it on Linux. Like me.
		__zsh_sudo_cmd=doas
	fi
fi

# Use the last command if the current line is empty.
if [[ -z $BUFFER ]]; then
	LBUFFER="$(fc -ln -1)"
fi

# Save whitespace preceding the command.
local WSPACE=${BUFFER%%[^[:space:]]*}
BUFFER="${BUFFER:${#WSPACE}}"
CURSOR=$(( $CURSOR - ${#WSPACE} ))

if [[ $BUFFER != "${__zsh_sudo_cmd} "* ]]; then
	LBUFFER="${__zsh_sudo_cmd} ${LBUFFER}"
fi

LBUFFER="${WSPACE}${LBUFFER}"
zle && zle redisplay
