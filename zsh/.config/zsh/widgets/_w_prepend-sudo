#!/usr/bin/env zsh
#######################################################################
# Fern's Dotfiles -- ZLE Widget - Prepend `sudo` to current command
# https://github.com/fernzi/dotfiles
#######################################################################

# I constantly forget that I've got this widget,
# and end up pressing home and typing it by hand.

if [[ ! -v __zsh_sudo_cmd ]]; then
	# Because of Systemd's takeover of Linux, the `run0` command
	# should almost always be available, even if it's not great.
	typeset -g __zsh_sudo_cmd=run0

	if [[ -v commands[doas] ]] && [[ -f /etc/doas.conf ]]; then
		# Use `doas` when it's available and correctly configured,
		# For OpenBSD and the weirdos who use it on Linux. Like me.
		__zsh_sudo_cmd=doas
	elif [[ -v commands[sudo] ]]; then
		# The real default. It's only here so it gets
		# picked over `run0` when both are installed.
		__zsh_sudo_cmd=sudo
	fi
fi

# Use the last command if the current line is empty.
if [[ -z $BUFFER ]]; then
	LBUFFER="$(fc -ln -1)"
fi

# Save whitespace preceding the command.
local WSPACE=${BUFFER%%[^[:space:]]*}
BUFFER="${BUFFER:${#WSPACE}}"
(( CURSOR -= ${#WSPACE} ))

if [[ $BUFFER != "${__zsh_sudo_cmd} "* ]]; then
	LBUFFER="${__zsh_sudo_cmd} ${LBUFFER}"
fi

LBUFFER="${WSPACE}${LBUFFER}"
zle && zle redisplay
